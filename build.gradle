plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "org.akhikhl.gretty" version "2.0.0"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"
}

apply plugin: 'kotlin'
apply plugin: 'war'

defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
    maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
    maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
    maven { url "https://dl.bintray.com/mvysny/vaadin-on-kotlin" }
    maven { url 'https://dl.bintray.com/webjars/maven' }
}

gretty {
    contextPath = ''
    httpPort = project.hasProperty("httpPort") ? project.httpPort as int : 8080
}

dependencyManagement {
    imports { mavenBom "com.vaadin:vaadin-bom:10.0.0.alpha15" }
}

dependencies {
    // Karibu-DSL dependency
    compile("com.github.vok.karibudsl:karibu-dsl-v10:0.2.21")

    // Vaadin
    compile "com.vaadin:vaadin-lumo-theme"
    compile "org.webjars.bower:polymer"
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // logging
    // currently we are logging through the SLF4J API to LogBack. See src/main/resources/logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.21"

    // test support
    testCompile "com.github.vok.karibudsl:karibu-testing-v10:0.2.21"
    testCompile "junit:junit:4.12"
    testCompile "org.jetbrains.kotlin:kotlin-test:1.2.21"

    // heroku app runner
    providedRuntime 'com.github.jsimone:webapp-runner:8.5.11.3'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Heroku
task stage(dependsOn: ['build'])
task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.providedRuntime) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)

